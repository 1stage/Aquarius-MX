100 REM Test DEF FN, FN()
110 QU=0:REM 0=Screen,1=Printer,2=File
130 GOSUB 900:REM Init
140 GOTO 200
150 ON QE GOTO 310,320,330,340,350
152 ON QE-5 GOTO 360,370,380,390,400
180 PRINT "Undefined Error Return";QE
190 REM

200 QT$="DEF FN":GOSUB 910

210 QU$="DEF FNSQ(X)=X*X":GOSUB 980
212 DEF FNSQ(X)=X*X
214 FOR I=0 TO 5 STEP .7:I$=STR$(I)
216 QA$="FNSQ("+I$+")="+I$+" *"+I$
218 GOSUB 920:NEXT

220 QU$="DEF FNSQ(X)=SQR(X)":GOSUB 980
222 DEF FNSQ(X)=SQR(X)
224 FOR I=0 TO 5:I$=STR$(I)
226 QA$="FNSQ("+I$+")=SQR("+I$+")"
228 GOSUB 920:NEXT

230 QU$="DEF FNCU(X)=X^3":GOSUB 980
232 DEF FNCU(X)=X^3
234 FOR I=0 TO 5 STEP .7:I$=STR$(I)
236 QA$="FNCU("+I$+")="+I$+"^3"
238 GOSUB 920:NEXT

240 QU$="DEF FNCR(X)=X^-3":GOSUB 980
242 DEF FNCR(X)=X^-3
244 FOR I=.1 TO 5:I$=STR$(I)
246 QA$="FNCR("+I$+")="+I$+"^-3"
248 GOSUB 920:NEXT

250 QU$="DEF FNY(X)=X*Y":GOSUB 980
251 QU$="Y=2":GOSUB 980
252 DEF FNY(X)=X*Y
254 FOR I=0 TO 5 STEP .7:I$=STR$(I)
256 QA$="FNY("+I$+")="+I$+" * Y"
258 GOSUB 920:NEXT

300 QE$="TM":QE=1:QA$="DEF FNS$(X)=STR$(X)"
305 ON ERROR GOTO 950:DEF FNS$(X)=STR$(X)

310 QE$="SN":QE=2:QA$="DEF FNSN=1"
315 ON ERROR GOTO 950:DEF FNSN=1

320 QE$="SN":QE=3:QA$="DEF FNSN()=1"
325 ON ERROR GOTO 950:DEF FNSN()=1

330 QE$="SN":QE=4:QA$="DEF FNSN(X=1"
335 ON ERROR GOTO 950:DEF FNSN(X=1

340 QE$="SN":QE=5:QA$="DEF FNSN(X)1"
345 ON ERROR GOTO 950:DEF FNSN(X)1

350 QU$="DEF FNB(X)=":GOSUB 980
352 DEF FNB(X)=
355 QE$="MO":QE=6:QA$="FNB(1)":GOSUB 940

360 QU$="DEF FNS(X)=STR$(X)":GOSUB 980
362 DEF FNS(X)=STR$(X)
365 QE$="TM":QE=7:QA$="FNS(1)":GOSUB 940

370 QU$="DEF FNS(X)=POKE":GOSUB 980
372 DEF FNS(X)=POKE
375 QE$="SN":QE=8:QA$="FNS(1)":GOSUB 940

380 REM

800 REM
810 QU$="Passed:"+STR$(QR(1))
815 GOSUB 980
820 QU$="Failed:"+STR$(QR(0))
825 GOSUB 980
870 END
880 IF QU THEN RETURN
882 PRINT "...Press a key..."
884 QL=0:QK=KEY(0)
886 IF QK=3 THEN STOP
888 RETURN
890 GOSUB 970:QU$=QA$:GOTO 980
900 QF$=FILE$:QF=LEN(FILE$):REM Init
902 IF QU>1 THEN DIM Q(999):QP=0
904 QU$="Running "+QF$:GOSUB 980
906 QF$=LEFT$(FILE$,QF-4)+".TST"
907 QR$(0)="Fail: ":QR$(1)="Pass: "
908 RETURN
910 QU$="Testing "+QT$:REM Print Title
912 GOTO 980
920 GOSUB 970:REM Do Assertion
922 ON ERROR GOTO 930
924 QV=-EVAL(QA$):ON ERROR GOTO 0
926 QU$=QR$(QV)+QA$:GOSUB 980
928 QR(QV)=QR(QV)+1:RETURN
930 QU$=ERR$(0)+" Error in "+QA$
932 GOSUB 980
938 END
940 GOSUB 970:REM Assert Error
942 ON ERROR GOTO 950
944 QV=EVAL(QA$):ON ERROR GOTO 0
946 QU$="No Error in "+QA$
948 GOSUB 980:QR(0)=QR(0)+1:RETURN
950 QU$=ERR$(0)+" Error in "+QA$
952 QV=-(ERR$(0)=QE$):QR(QV)=QR(QV)+1
954 QU$=QR$(QV)+QU$:GOSUB 980
956 CLEAR ERR:ON ERROR GOTO 0
958 GOTO 150
960 REM so we can RETURN from Error
970 QA$=QA$+" ":REM Convert ` to "
972 FOR QI=0 TO LEN(QA$)-1
974 QC=PEEK(&&QA$+QI)
976 IF QC=96 THEN POKE &&QA$+QI,34
978 NEXT:RETURN
980 IF QU GOTO 988:REM Output Line
982 IF QL>20 THEN GOSUB 880
984 PRINT QU$:QL=QL+1:RETURN
988 IF QU=1 THEN LPRINT QU$:RETURN
990 QN=LEN(QU$)+2
992 POKE &Q(0)+QP,QU$
994 QP=QP+QN:POKE &Q(0)-1,13,10
996 RETURN
