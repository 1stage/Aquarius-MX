100 REM Test READ/DATA
110 QU=0:REM 0=Screen,1=Printer,2=File
130 GOSUB 900:REM Init
140 GOTO 200
150 ON QE GOTO 340,440,540,740,770
180 PRINT "Undefined Error Return";QE
190 REM

200 QT$="READ Numeric":GOSUB 910
210 QU$="READ A,B,C,D":GOSUB 980
215 READ A,B,C,D
220 QA$="A=1":GOSUB 920
222 QA$="B=2":GOSUB 920
224 QA$="C=3.1415":GOSUB 920
226 QA$="D=186000":GOSUB 920
250 QU$="READ A$,B$,C$,D$":GOSUB 980
255 RESTORE 290:READ A$,B$,C$,D$
260 QA$="A$=`1`":GOSUB 920
262 QA$="B$=`2`":GOSUB 920
264 QA$="C$=`3.1415`":GOSUB 920
266 QA$="D$=`186E3`":GOSUB 920
290 DATA 1,2,3.1415,186E3

300 QT$="READ String":GOSUB 910
310 QU$="READ A$,B$,C$,D$":GOSUB 980
315 READ A$,B$,C$,D$
320 QA$="A$=`ABC`":GOSUB 920
322 QA$="B$=`D E F `":GOSUB 920
324 QA$="C$=`1,2,3`":GOSUB 920
326 QA$="D$=`X Y Z`":GOSUB 920
330 QA$="READ E":QE=1:QE$="SN"
332 ON ERROR GOTO 950
334 READ E:GOSUB 944
340 QU$="READ E$":GOSUB 980
345 READ E$
350 QA$="E$=`NaN`":GOSUB 920
390 DATA ABC,D E F ,"1,2,3",X Y Z,NaN

400 QT$="READ Hex":GOSUB 910
410 QU$="READ A,B,C,D":GOSUB 980
415 READ A,B,C,D
420 QA$="A=17":GOSUB 920
422 QA$="B=26":GOSUB 920
424 QA$="C=65535":GOSUB 920
426 QA$="D=1":GOSUB 920
430 QA$="READ E$":QE=2:QE$="TM"
432 ON ERROR GOTO 950
434 READ E$:GOSUB 944
440 QU$="READ E":GOSUB 980
445 READ E
450 QA$="E=4097":GOSUB 920
490 DATA $11,$1A,$FFFF,$0000001,$1001

500 QT$="READ Hex String":GOSUB 910
510 QU$="READ A$,B$,C$,D$":GOSUB 980
515 READ A$,B$,C$,D$
520 QA$="A$=`123`":GOSUB 920
522 QA$="B$=`@`":GOSUB 920
524 QA$="C$=``":GOSUB 920
526 QA$="D$=CHR$(0)":GOSUB 920
530 QA$="READ E":QE=3:QE$="SN"
532 ON ERROR GOTO 950
534 READ E:GOSUB 944
540 QU$="READ E$":GOSUB 980
545 READ E$
550 QA$="E$=` `":GOSUB 920
590 DATA $"313233",$"40",$"",$"00",$"20"

700 QT$="Out of Data":GOSUB 910
710 QA$="READ E":QE=4:QE$="OD"
720 ON ERROR GOTO 950
730 READ E:GOSUB 944
740 QA$="READ E$":QE=5:QE$="OD"
750 ON ERROR GOTO 950
760 READ E$:GOSUB 944
770 REM

800 REM
810 QU$="Passed:"+STR$(QR(1))
815 GOSUB 980
820 QU$="Failed:"+STR$(QR(0))
825 GOSUB 980
870 END
880 IF QU THEN RETURN
882 PRINT "...Press a key."
884 QL=0:QK=KEY(0)
886 IF QK=3 THEN STOP
888 RETURN
890 GOSUB 970:QU$=QA$:GOTO 980
900 QR$(0)="Fail: ":QR$(1)="Pass: "
902 QF$=FILE$:QF=LEN(FILE$)
904 QU$="Running "+QF$
905 IF QU<2 GOTO 980
906 QF$=LEFT$(FILE$,QF-4)+".TST"
907 ON ERROR GOTO 909:DEL QF$
908 CLEAR ERR:ON ERROR GOTO 0
909 GOSUB 980:GOTO 140
910 QU$="Testing "+QT$:REM Print Title
912 GOTO 980
920 GOSUB 970:REM Do Assertion
922 ON ERROR GOTO 930
924 QV=-EVAL(QA$):ON ERROR GOTO 0
926 QU$=QR$(QV)+QA$:GOSUB 980
928 QR(QV)=QR(QV)+1:RETURN
930 QU$=ERR$(0)+" Error in "+QA$
932 GOSUB 980
938 END
940 GOSUB 970:REM Assert Error
942 ON ERROR GOTO 950:QV=EVAL(QA$)
944 ON ERROR GOTO 0
946 QU$="No Error in "+QA$
948 GOSUB 980:QR(0)=QR(0)+1:RETURN
950 QU$=ERR$(0)+" Error in "+QA$
952 QV=-(ERR$(0)=QE$):QR(QV)=QR(QV)+1
954 QU$=QR$(QV)+QU$:GOSUB 980
956 CLEAR ERR:ON ERROR GOTO 0:GOTO 150
960 REM so we can RETURN from Error
970 QA$=QA$+" ":REM Convert ` to "
972 FOR QI=0 TO LEN(QA$)-1
974 QC=PEEK(&&QA$+QI)
976 IF QC=96 THEN POKE &&QA$+QI,34
978 NEXT:RETURN
980 IF QU GOTO 988:REM Output Line
982 IF QL>20 THEN GOSUB 880
984 PRINT QU$:QL=QL+1:RETURN
988 IF QU=1 THEN LPRINT QU$:RETURN
990 QN=LEN(QU$)+2:REM Output to File
992 QU$=QU$+CHR$(13)+CHR$(10)
994 SAVE QF$,&&QU$,QN,$FFFF
996 RETURN
