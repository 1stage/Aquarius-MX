100 REM Test ASC(), ASC$(), DEC() functions
110 QU=0:REM 0=Screen,1=Printer,2=File
130 GOSUB 900:REM Init
140 GOTO 200
150 ON QE GOTO 300,340,350,440
180 PRINT "Undefined Error Return";QE
190 REM
200 QT$="ASC(CHR$())":GOSUB 910
210 FOR A=0 TO 255 STEP 33
215 A$=MID$(STR$(A),2)
220 QA$="ASC(CHR$("+A$+"))="+A$
225 GOSUB 920:NEXT
250 QT$="ASC(string)":GOSUB 910
260 QA$="ASC(`AQUARIUS`)=65"
265 GOSUB 920
270 QA$="ASC(`zeddy`)=122"
275 GOSUB 920
280 QT$="ASC(number)":GOSUB 910
290 QA$="ASC(123)":QE$="TM":QE=1
295 GOSUB 940
300 QT$="ASC$(string)":GOSUB 910
310 QA$="ASC$(``)=``"
315 GOSUB 920
320 QA$="ASC$(`2021`)=` !`"
325 GOSUB 920
330 QA$="ASC$(`123`)":QE$="FC":QE=2
335 GOSUB 940
340 QA$="ASC$(`BCDEFG`)":QE$="FC":QE=3
345 GOSUB 940
350 REM
400 QT$="DEC(string)":GOSUB 910
410 QA$="DEC(``)=0":GOSUB 920
412 QA$="DEC(`0`)=0":GOSUB 920
414 QA$="DEC(`00`)=0":GOSUB 920
416 QA$="DEC(`000`)=0":GOSUB 920
418 QA$="DEC(`0000`)=0":GOSUB 920
420 QA$="DEC(`1`)=1":GOSUB 920
422 QA$="DEC(`11`)=17":GOSUB 920
424 QA$="DEC(`111`)=273":GOSUB 920
426 QA$="DEC(`1111`)=4369":GOSUB 920
428 QA$="DEC(`01111`)=4369":GOSUB 920
430 QA$="DEC(`11111`)":QE$="OV":QE=4
435 GOSUB 940
440 A$="FF"+"":B$="FF"+""
442 QU$="A$=`"+A$+"`":GOSUB 980
444 QU$="B$=`"+B$+"`":GOSUB 980
446 QA$="DEC(A$)=255":GOSUB 920
448 QA$="DEC(B$)=255":GOSUB 920
450 QA$="DEC(`   1`)=1":GOSUB 920
452 QA$="DEC(`  12`)=18":GOSUB 920
454 QA$="DEC(` 123`)=291":GOSUB 920
456 QA$="DEC(`1234`)=4660":GOSUB 920
458 QA$="DEC(`2345`)=9029":GOSUB 920
460 QA$="DEC(`9`)=9":GOSUB 920
462 QA$="DEC(`9A`)=154":GOSUB 920
464 QA$="DEC(`9AB`)=2475":GOSUB 920
466 QA$="DEC(`9ABC`)=39612":GOSUB 920
468 QA$="DEC(`ABCD`)=43981":GOSUB 920

800 REM
810 PRINT "Passed:";QR(1)
820 PRINT "Failed:";QR(0)
890 END
900 QF$=FILE$:QF=LEN(FILE$):REM Init
902 IF QU>1 THEN DIM Q(999):QP=0
904 QU$="Running "+QF$:GOSUB 980
906 QF$=LEFT$(FILE$,QF-4)+".TST"
907 QR$(0)="Fail: ":QR$(1)="Pass: "
908 RETURN
910 QU$="Testing "+QT$:REM Print Title
912 GOTO 980
920 GOSUB 970:REM Do Assertion
922 ON ERROR GOTO 930
924 QV=-EVAL(QA$):ON ERROR GOTO 0
926 QU$=QR$(QV)+QA$:GOSUB 980
928 QR(QV)=QR(QV)+1:RETURN
930 QU$=ERR$(0)+" Error in "+QA$
932 GOSUB 980
938 END
940 GOSUB 970:REM Assert Error
942 ON ERROR GOTO 950
944 QV=EVAL(QA$):ON ERROR GOTO 0
946 QU$="No Error in "+QA$
948 GOSUB 980:QR(0)=QR(0)+1:RETURN
950 QU$=ERR$(0)+" Error in "+QA$
952 QV=-(ERR$(0)=QE$):QR(QV)=QR(QV)+1
954 QU$=QR$(QV)+QU$:GOSUB 980
956 CLEAR ERR:ON ERROR GOTO 0
958 GOTO 150
960 REM so we can RETURN from Error
970 QA$=QA$+" ":REM Convert ` to "
972 FOR QI=0 TO LEN(QA$)-1
974 QC=PEEK(&&QA$+QI)
976 IF QC=96 THEN POKE &&QA$+QI,34
978 NEXT:RETURN
980 IF QU GOTO 988:REM Output Line
982 IF QL>20 THEN QK=KEY(0):QL=0
984 PRINT QU$:QL=QL+1:RETURN
988 IF QU=1 THEN LPRINT QU$:RETURN
990 QN=LEN(QU$)+2
992 POKE &Q(0)+QP,QU$
994 QP=QP+QN:POKE &Q(0)-1,13,10
996 RETURN
