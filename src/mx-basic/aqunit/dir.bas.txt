100 REM Test DIR, CAT, CD, CD$
110 QU=0:REM 0=Screen,1=Printer,2=File
130 GOSUB 900:REM Init
140 GOTO 200
150 ON QE GOTO 
180 PRINT "Undefined Error Return";QE
190 REM

200 CLS:DIR"*."
205 PRINT:PRINT
210 QT$="DIR Root name":GOSUB 910
220 QA$="PEEK$(12369,8)=`..      `":GOSUB 920
225 QA$="PEEK$(12382,5)=`<dir>`":GOSUB 920

300 GOSUB 880:CLS:DIR"AQUNIT.TXT"
305 PRINT:PRINT
310 QT$="DIR File name":GOSUB 910 
320 QA$="PEEK$(12369,8)=`AQUNIT  `":GOSUB 920
325 QA$="PEEK$(12378,3)=`TXT`":GOSUB 920
330 QA$="PEEK$(12382,5)=` 618 `":GOSUB 920

400 GOSUB 880:CLS:CAT
405 PRINT:PRINT
410 QT$="CAT Root name":GOSUB 910
420 QA$="PEEK$(12329,12)=`<..        >`":GOSUB 920

500 GOSUB 880:CLS:CAT
505 PRINT:PRINT
510 QT$="CAT File name":GOSUB 910
520 QA$="PEEK$(12355,12)=`AQUNIT   TXT`":GOSUB 920

600 GOSUB 880:CLS
610 QT$="CD and CD$":GOSUB 910
620 D$="/":GOSUB 840
625 QA$="CD$=`/`":GOSUB 920
630 D$="AQUNIT":GOSUB 840
635 QA$="CD$=`/AQUNIT`":GOSUB 920

700 GOSUB 880
705 CLS
710 QT$="MKDIR":GOSUB 980
715 ON ERROR GOTO 750
720 CD "TESTER"
725 PRINT "Folder TESTER already exists..."
730 PRINT "Delete folder manually and"
735 PRINT "run DIR.BAS again."
740 PRINT "Skipping MKDIR tests."
745 CD "..":GOTO 800:REM Skip MKDIR tests
750 CLEAR ERR:ON ERROR GOTO 0:REM TESTER folder does not exist
755 D$="TESTER":MKDIR D$:GOSUB 840
760 QA$="CD$=`/AQUNIT/TESTER`":GOSUB 920
765 D$="..":GOSUB 840
770 QA$="CD$=`/AQUNIT`":GOSUB 920

800 REM
810 QU$="Passed:"+STR$(QR(1))
815 GOSUB 980
820 QU$="Failed:"+STR$(QR(0))
825 GOSUB 980
830 END
840 QA$="CD `"+D$+"`":GOSUB 890
842 ON ERROR GOTO 930
844 CD D$
846 ON ERROR GOTO 0
848 RETURN
880 IF QU THEN RETURN
882 PRINT "...Press a key..."
884 QL=0:QK=KEY(0)
886 IF QK=3 THEN STOP
888 RETURN
890 GOSUB 970:QU$=QA$:GOTO 980
900 QF$=FILE$:QF=LEN(FILE$):REM Init
902 IF QU>1 THEN DIM Q(999):QP=0
904 QU$="Running "+QF$:GOSUB 980
906 QF$=LEFT$(FILE$,QF-4)+".TST"
907 QR$(0)="Fail: ":QR$(1)="Pass: "
908 RETURN
910 QU$="Testing "+QT$:REM Print Title
912 GOTO 980
920 GOSUB 970:REM Do Assertion
922 ON ERROR GOTO 930
924 QV=-EVAL(QA$):ON ERROR GOTO 0
926 QU$=QR$(QV)+QA$:GOSUB 980
928 QR(QV)=QR(QV)+1:RETURN
930 QU$=ERR$(0)+" Error in "+QA$
932 GOSUB 980
938 END
940 GOSUB 970:REM Assert Error
942 ON ERROR GOTO 950
944 QV=EVAL(QA$):ON ERROR GOTO 0
946 QU$="No Error in "+QA$
948 GOSUB 980:QR(0)=QR(0)+1:RETURN
950 QU$=ERR$(0)+" Error in "+QA$
952 QV=-(ERR$(0)=QE$):QR(QV)=QR(QV)+1
954 QU$=QR$(QV)+QU$:GOSUB 980
956 CLEAR ERR:ON ERROR GOTO 0
958 GOTO 150
960 REM so we can RETURN from Error
970 QA$=QA$+" ":REM Convert ` to "
972 FOR QI=0 TO LEN(QA$)-1
974 QC=PEEK(&&QA$+QI)
976 IF QC=96 THEN POKE &&QA$+QI,34
978 NEXT:RETURN
980 IF QU GOTO 988:REM Output Line
982 IF QL>20 THEN GOSUB 880
984 PRINT QU$:QL=QL+1:RETURN
988 IF QU=1 THEN LPRINT QU$:RETURN
990 QN=LEN(QU$)+2
992 POKE &Q(0)+QP,QU$
994 QP=QP+QN:POKE &Q(0)-1,13,10
996 RETURN
