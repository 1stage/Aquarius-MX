100 REM Test DIR, CAT, CD, CD$
110 QU=0:REM 0=Screen,1=Printer,2=File
130 GOSUB 900:REM Init
140 GOTO 200
150 ON QE GOTO 
180 PRINT "Undefined Error Return";QE
190 REM

200 QT$="DIR Root name":GOSUB 910
205 CLS
210 DIR"*."
215 QA$="PEEK$(12369,8)=`..      `":GOSUB 920
220 QA$="PEEK$(12382,5)=`<dir>`":GOSUB 920

299 PRINT "Press any key to continue..."
300 QL=0:K=KEY(0)
305 QT$="DIR File name":GOSUB 910
310 CLS
315 DIR"AQUNIT.TXT"
320 QA$="PEEK$(12369,8)=`AQUNIT  `":GOSUB 920
325 QA$="PEEK$(12378,3)=`TXT`":GOSUB 920
330 QA$="PEEK$(12382,5)=` 618 `":GOSUB 920

399 PRINT "Press any key to continue..."
400 QL=0:K=KEY(0)
405 QT$="CAT Root name":GOSUB 910
410 CLS
415 CAT
420 QA$="PEEK$(12329,12)=`<..        >`":GOSUB 920

499 PRINT "Press any key to continue..."
500 QL=0:K=KEY(0)
505 QT$="CAT File name":GOSUB 910
510 CLS
515 CAT
520 QA$="PEEK$(12355,12)=`AQUNIT   TXT`":GOSUB 920

599 PRINT "Press any key to continue..."
600 QL=0:K=KEY(0)
605 QT$="CD and CD$":GOSUB 910
610 CLS
615 QA$="CD$=`/AQUNIT`":GOSUB 980
620 CD "/"
625 QA$="CD$=`/`":GOSUB 920
630 CD "AQUNIT"
635 QA$="CD$=`/AQUNIT`":GOSUB 920

699 PRINT "Press any key to continue..."
700 QL=0:K=KEY(0)
705 CLS
710 QT$="MKDIR":GOSUB 980
715 ON ERROR GOTO 750
720 CD "TESTER"
725 PRINT "Folder TESTER already exists..."
730 PRINT "Delete folder manually and"
735 PRINT "run DIR.BAS again."
740 PRINT "Skipping MKDIR tests."
745 CD "..":GOTO 800:REM Skip MKDIR tests
750 REM TESTER folder does not exists
755 MKDIR "TESTER":CD "TESTER"
760 QA$="CD$=`/AQUNIT/TESTER`":GOSUB 920
765 CD ".."
770 QA$="CD$=`/AQUNIT`":GOSUB 920

800 REM
810 PRINT "Passed:";QR(1)
820 PRINT "Failed:";QR(0)
880 END
890 GOSUB 970:QU$=QA$:GOTO 980
900 QF$=FILE$:QF=LEN(FILE$):REM Init
902 IF QU>1 THEN DIM Q(999):QP=0
904 QU$="Running "+QF$:GOSUB 980
906 QF$=LEFT$(FILE$,QF-4)+".TST"
907 QR$(0)="Fail: ":QR$(1)="Pass: "
908 RETURN
910 QU$="Testing "+QT$:REM Print Title
912 GOTO 980
920 GOSUB 970:REM Do Assertion
922 ON ERROR GOTO 930
924 QV=-EVAL(QA$):ON ERROR GOTO 0
926 QU$=QR$(QV)+QA$:GOSUB 980
928 QR(QV)=QR(QV)+1:RETURN
930 QU$=ERR$(0)+" Error in "+QA$
932 GOSUB 980
938 END
940 GOSUB 970:REM Assert Error
942 ON ERROR GOTO 950
944 QV=EVAL(QA$):ON ERROR GOTO 0
946 QU$="No Error in "+QA$
948 GOSUB 980:QR(0)=QR(0)+1:RETURN
950 QU$=ERR$(0)+" Error in "+QA$
952 QV=-(ERR$(0)=QE$):QR(QV)=QR(QV)+1
954 QU$=QR$(QV)+QU$:GOSUB 980
956 CLEAR ERR:ON ERROR GOTO 0
958 GOTO 150
960 REM so we can RETURN from Error
970 QA$=QA$+" ":REM Convert ` to "
972 FOR QI=0 TO LEN(QA$)-1
974 QC=PEEK(&&QA$+QI)
976 IF QC=96 THEN POKE &&QA$+QI,34
978 NEXT:RETURN
980 IF QU GOTO 988:REM Output Line
982 IF QL>20 THEN QL=0:QK=KEY(0)
984 PRINT QU$:QL=QL+1:RETURN
988 IF QU=1 THEN LPRINT QU$:RETURN
990 QN=LEN(QU$)+2
992 POKE &Q(0)+QP,QU$
994 QP=QP+QN:POKE &Q(0)-1,13,10
996 RETURN
