100 REM Test PEEK(), $ and &
110 QU=0:REM 0=Screen,1=Printer,2=File
130 GOSUB 900:REM Init
140 GOTO 200
150 ON ERROR GOTO 0
155 ON QE GOTO 220,240,350,570
180 PRINT "Undefined Error Return";QE
190 REM
200 QT$="PEEK()":GOSUB 910
212 QA$="PEEK(3)=$82":GOSUB 920
214 QA$="PEEK($3821)=$35":GOSUB 920
216 QA$="PEEK(57344)=66":GOSUB 920
218 QA$="PEEK(`123`)":QE$="TM":QE=1:GOSUB 940
220 REM
230 QT$="PEEK$()":GOSUB 910
232 QA$="PEEK$($373,5)=`Break`":GOSUB 920
238 QA$="PEEK$(`123`)":QE$="TM":QE=2:GOSUB 940
240 REM
250 QT$="$"+CHR$(34)+"hex"+CHR$(34):GOSUB 910
252 QA$="$`414243`=`ABC`":GOSUB 920
254 QA$="$`3132333435`=`12345`":GOSUB 920
300 QT$="&numeric":GOSUB 910
310 A=12345
312 QU$="A ="+STR$(A):GOSUB 980
314 B=A:
316 QU$="B ="+STR$(B):GOSUB 980
320 QA$="PEEK$(&A,4)=PEEK$(&B,4)"
325 GOSUB 920
330 XY=123:QA$="PEEK$(&XY-2,2)=`XY`"
335 GOSUB 920
340 QA$="&&A":QE$="TM":QE=3
345 GOSUB 940
350 REM

400 QT$="&string":GOSUB 910
410 A$="ABC":QA$="A$=`"+A$+"`"
415 GOSUB 890
420 QA$="HEX$(PEEK$(&A$-2,2))=`4180`"
425 GOSUB 920
430 QA$="PEEK(&A$)=LEN(A$)"
435 GOSUB 920
440 QA$="DEEK(&A$+2)=&&A$"
445 GOSUB 920
450 QA$="PEEK$(&&A$,PEEK(&A$))=A$"
455 GOSUB 920

500 QT$="&array":GOSUB 910
510 DIM R(3,4):QU$="DIM R(3,4)
515 GOSUB 980
520 QA$="PEEK(&*R-2)=82"
525 GOSUB 920
530 QA$="PEEK(&*R+2)=2"
535 GOSUB 920
540 QA$="DEEK(&*R+3)=4+1"
545 GOSUB 920
550 QA$="DEEK(&*R+5)=3+1"
555 GOSUB 920
560 QA$="&&*R":QE$="SN":QE=4
565 GOSUB 940
570 REM

800 REM
810 QU$="Passed:"+STR$(QR(1))
815 GOSUB 980
820 QU$="Failed:"+STR$(QR(0))
825 GOSUB 980
870 END
880 IF QU THEN RETURN
882 PRINT "...Press a key..."
884 QL=0:QK=KEY(0)
886 IF QK=3 THEN STOP
888 RETURN
890 GOSUB 970:QU$=QA$:GOTO 980
900 QF$=FILE$:QF=LEN(FILE$):REM Init
902 IF QU>1 THEN DIM Q(999):QP=0
904 QU$="Running "+QF$:GOSUB 980
906 QF$=LEFT$(FILE$,QF-4)+".TST"
907 QR$(0)="Fail: ":QR$(1)="Pass: "
908 RETURN
910 QU$="Testing "+QT$:REM Print Title
912 GOTO 980
920 GOSUB 970:REM Do Assertion
922 ON ERROR GOTO 930
924 QV=-EVAL(QA$):ON ERROR GOTO 0
926 QU$=QR$(QV)+QA$:GOSUB 980
928 QR(QV)=QR(QV)+1:RETURN
930 QU$=ERR$(0)+" Error in "+QA$
932 GOSUB 980
938 END
940 GOSUB 970:REM Assert Error
942 ON ERROR GOTO 950
944 QV=EVAL(QA$):ON ERROR GOTO 0
946 QU$="No Error in "+QA$
948 GOSUB 980:QR(0)=QR(0)+1:RETURN
950 QU$=ERR$(0)+" Error in "+QA$
952 QV=-(ERR$(0)=QE$):QR(QV)=QR(QV)+1
954 QU$=QR$(QV)+QU$:GOSUB 980
956 RESUME 150
960 REM so we can RETURN from Error
970 QA$=QA$+" ":REM Convert ` to "
972 FOR QI=0 TO LEN(QA$)-1
974 QC=PEEK(&&QA$+QI)
976 IF QC=96 THEN POKE &&QA$+QI,34
978 NEXT:RETURN
980 IF QU GOTO 988:REM Output Line
982 IF QL>20 THEN GOSUB 880
984 PRINT QU$:QL=QL+1:RETURN
988 IF QU=1 THEN LPRINT QU$:RETURN
990 QN=LEN(QU$)+2
992 POKE &Q(0)+QP,QU$
994 QP=QP+QN:POKE &Q(0)-1,13,10
996 RETURN
