100 REM Test GET, PUT, CLS
110 QU=0:REM 0=Screen,1=Printer,2=File
130 GOSUB 900:REM Init
150 ON ERROR GOTO 0
152 ON QE GOTO 
180 PRINT "Undefined Error Return";QE
190 REM

200 A=$3400:A$=STR$(A):
201 B=A+999:B$=STR$(B)
202 C=B+1:C$=STR$(C)
204 POKE C,$99
206 CLS 7,0
208 P0=POS(0):P1=POS(1):P2=POS(2):P3=POS(3):P4=POS(4)
209 G1=POS(-1):G2=POS(-2):G3=POS(-3)
210 QT$="CLS":GOSUB 910
212 QU$="POKE"+C$+",$99:CLS 7,0":GOSUB 980
220 QU$="P0=POS(0)":GOSUB 980
222 QU$="P1=POS(1):P2=POS(2)":GOSUB 980
224 QU$="P3=POS(3):P4=POS(4)":GOSUB 980
226 QU$="G1=POS(-1):G2=POS(-2)":GOSUB 980
228 QU$="G3=POS(-3)":GOSUB 980
230 QA$="PEEK("+A$+")=$70":GOSUB 920
232 QA$="PEEK("+B$+")=$70":GOSUB 920
234 QA$="PEEK("+C$+")=$99":GOSUB 920
240 QA$="P0=0":GOSUB 920
242 QA$="P1=1":GOSUB 920
244 QA$="P2=0":GOSUB 920
246 QA$="P3=12329":GOSUB 920
248 QA$="P4=13353":GOSUB 920
252 QA$="G1=0":GOSUB 920
254 QA$="G2=0":GOSUB 920
256 QA$="G3=12328":GOSUB 920
260 QA$="COLOR(0)=7":GOSUB 920
262 QA$="COLOR(1)=7":GOSUB 920

300 IF QU=0 THEN QL=2:K=KEY(0):CLS 7,0:PRINT:PRINT
305 QT$="GET":GOSUB 910
310 A=$3000+72:A$=STR$(A)
311 C=A+1024:C$=STR$(C)
312 B1$="D7C9":H1$="$`"+B1$+"`":A1$=ASC$(B1$)
314 B2$="C7D9":H2$="$`"+B2$+"`":A2$=ASC$(B2$)
316 B3$="1020":H3$="$`"+B3$+"`":A3$=ASC$(B3$)
318 B4$="3040":H4$="$`"+B4$+"`":A4$=ASC$(B4$)
320 QA$="POKE"+A$+","+H1$+",STEP 40,"+H2$ 
322 POKE A,A1$,STEP 40,A2$:GOSUB 890
324 QA$="POKE"+C$+","+H3$+",STEP 40,"+H4$ 
326 POKE C,A3$,STEP 40,A4$:GOSUB 890
330 DIM B(2):QU$="DIM B(2)":GOSUB 980
332 QU$="GET (32,0)-(33,1),B"
334 GET (32,0)-(33,1),B:GOSUB 980
340 B$="02000200D710C920C730D940"
342 H$="$`"+B$+"`":D$=ASC$(B$)
344 QA$="PEEK$(&B(0),12)="+H$:GOSUB 920
350 QU$="PUT (36,2),B":PUT (36,2),B:GOSUB 980
352 QA$="PEEK$(12444,2)="+H1$:GOSUB 920
354 QA$="PEEK$(12484,2)="+H2$:GOSUB 920
356 QA$="PEEK$(13468,2)="+H3$:GOSUB 920
358 QA$="PEEK$(13508,2)="+H4$:GOSUB 920

400 QT$="PUT":GOSUB 910
402 B$="020001002A3D2A3D"
404 QA$="POKE &EB(0),$`"+B$+"`":GOSUB 890
406 POKE &B(0),ASC$(B$)
408 IF QU=0 THEN QK=KEY(0):QL=0
410 CLS 7,0:QU$="CLS 7,0":GOSUB 980
412 QU$="PUT (0,38),B,PSET":GOSUB 980
414 PUT (38,0),B,PSET
416 QA$="PEEK$(13390,2)=$`3D3D`"
418 GOSUB 920
422 QU$="PUT (0,38),B,PRESET":GOSUB 980
424 PUT (38,2),B,PRESET
426 QA$="PEEK$(13470,2)=$`C2C2`"
428 GOSUB 920
432 QU$="PUT (0,38),B,OR":GOSUB 980
434 PUT (38,4),B,OR
436 QA$="PEEK$(13550,2)=$`7D7D`"
438 GOSUB 920
442 QU$="PUT (0,38),B,AND":GOSUB 980
444 PUT (38,6),B,AND
446 QA$="PEEK$(13630,2)=$`3030`"
448 GOSUB 920
452 QU$="PUT (0,38),B,XOR":GOSUB 980
454 PUT (38,8),B,XOR
456 QA$="PEEK$(13710,2)=$`4D4D`"
458 GOSUB 920
462 QU$="PUT (0,38),B,SWAP":GOSUB 980
464 PUT (38,10),B,SWAP
466 QA$="PEEK$(13790,2)=$`D3D3`"
468 GOSUB 920

800 REM
810 PRINT "Passed:";QR(1)
820 PRINT "Failed:";QR(0)
870 END
890 GOSUB 970:QU$=QA$:GOTO 980
900 QF$=FILE$:QF=LEN(FILE$):REM Init
902 IF QU>1 THEN DIM Q(999):QP=0
904 QU$="Running "+QF$:GOSUB 980
906 QF$=LEFT$(FILE$,QF-4)+".TST"
907 QR$(0)="Fail: ":QR$(1)="Pass: "
908 RETURN
910 QU$="Testing "+QT$:REM Print Title
912 GOTO 980
920 GOSUB 970:REM Do Assertion
922 ON ERROR GOTO 930
924 QV=-EVAL(QA$):ON ERROR GOTO 0
926 QU$=QR$(QV)+QA$:GOSUB 980
928 QR(QV)=QR(QV)+1:RETURN
930 QU$=ERR$(0)+" Error in "+QA$
932 GOSUB 980
938 END
940 GOSUB 970:REM Assert Error
942 ON ERROR GOTO 950
944 QV=EVAL(QA$):ON ERROR GOTO 0
946 QU$="No Error in "+QA$
948 GOSUB 980:QR(0)=QR(0)+1:RETURN
950 QU$=ERR$(0)+" Error in "+QA$
952 QV=-(ERR$(0)=QE$):QR(QV)=QR(QV)+1
954 QU$=QR$(QV)+QU$:GOSUB 980
956 RESUME 150
960 REM so we can RETURN from Error
970 QA$=QA$+" ":REM Convert ` to "
972 FOR QI=0 TO LEN(QA$)-1
974 QC=PEEK(&&QA$+QI)
976 IF QC=96 THEN POKE &&QA$+QI,34
978 NEXT:RETURN
980 IF QU GOTO 988:REM Output Line
982 IF QL>20 THEN QK=KEY(0):QL=0
984 PRINT QU$:QL=QL+1:RETURN
988 IF QU=1 THEN LPRINT QU$:RETURN
990 QN=LEN(QU$)+2
992 POKE &Q(0)+QP,QU$
994 QP=QP+QN:POKE &Q(0)-1,13,10
996 RETURN
