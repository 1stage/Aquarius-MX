100 REM Test CALL, USR(), DEBUG
110 QU=0:REM 0=Screen,1=Printer,2=File
130 GOSUB 900:REM Init
140 GOTO 200
150 ON QE GOTO 420
180 PRINT "Undefined Error Return";QE
190 REM

200 CLS:DEBUG
205 QT$="DEBUG":GOSUB 910
210 M$="Debugger not installed"
212 L=LEN(M$):L$=STR$(L)
214 QU$="DEBUG":GOSUB 980
216 QA$="PEEK$(12329,"+L$+")=`"+M$+"`"
218 GOSUB 920

300 QT$="CALL":GOSUB 910
310 M$="3E40320130C9"
312 POKE &M(0),ASC$(M$)
314 QA$="POKE &M(0),$`"+M$+"`":GOSUB 890
320 QU$="CALL &M(0)":GOSUB 980
322 CALL &M(0)
334 QA$="PEEK($3001)=64":GOSUB 920

400 QT$="USR":GOSUB 910
405 UA=DEEK(14340):REM Save Default
410 QE=1:QE$="FC":QA$="USR(0)"
415 GOSUB 940
420 QU$="DEF USR=$566":GOSUB 980
422 DEF USR=$566
424 QA$="DEEK(14340)=1382":GOSUB 920
426 QA$="USR(123)=123":GOSUB 920
428 QA$="USR(-1/2)=-1/2":GOSUB 920
430 QU$="DEF USR=$150B":GOSUB 980
432 DEF USR=$150B
434 QA$="DEEK(14340)=5387":GOSUB 920
436 QA$="USR(456)=-456":GOSUB 920
438 QA$="USR(1/3)=-1/3":GOSUB 920
440 DOKE 14340,UA:REM Restore Default

800 REM
810 QU$="Passed:"+STR$(QR(1))
815 GOSUB 980
820 QU$="Failed:"+STR$(QR(0))
825 GOSUB 980
870 END
880 IF QU THEN RETURN
882 PRINT "...Press a key."
884 QL=0:QK=KEY(0)
886 IF QK=3 THEN STOP
888 RETURN
890 GOSUB 970:QU$=QA$:GOTO 980
900 QR$(0)="Fail: ":QR$(1)="Pass: "
902 QF$=FILE$:QF=LEN(FILE$)
904 QU$="Running "+QF$
905 IF QU<2 GOTO 980
906 QF$=LEFT$(FILE$,QF-4)+".TST"
907 ON ERROR GOTO 909:DEL QF$
908 CLEAR ERR:ON ERROR GOTO 0
909 GOSUB 980:GOTO 140
910 QU$="Testing "+QT$:REM Print Title
912 GOTO 980
920 GOSUB 970:REM Do Assertion
922 ON ERROR GOTO 930
924 QV=-EVAL(QA$):ON ERROR GOTO 0
926 QU$=QR$(QV)+QA$:GOSUB 980
928 QR(QV)=QR(QV)+1:RETURN
930 QU$=ERR$(0)+" Error in "+QA$
932 GOSUB 980
938 END
940 GOSUB 970:REM Assert Error
942 ON ERROR GOTO 950
944 QV=EVAL(QA$):ON ERROR GOTO 0
946 QU$="No Error in "+QA$
948 GOSUB 980:QR(0)=QR(0)+1:RETURN
950 QU$=ERR$(0)+" Error in "+QA$
952 QV=-(ERR$(0)=QE$):QR(QV)=QR(QV)+1
954 QU$=QR$(QV)+QU$:GOSUB 980
956 CLEAR ERR:ON ERROR GOTO 0:GOTO 150
960 REM so we can RETURN from Error
970 QA$=QA$+" ":REM Convert ` to "
972 FOR QI=0 TO LEN(QA$)-1
974 QC=PEEK(&&QA$+QI)
976 IF QC=96 THEN POKE &&QA$+QI,34
978 NEXT:RETURN
980 IF QU GOTO 988:REM Output Line
982 IF QL>20 THEN GOSUB 880
984 PRINT QU$:QL=QL+1:RETURN
988 IF QU=1 THEN LPRINT QU$:RETURN
990 QN=LEN(QU$)+2:REM Output to File
992 QU$=QU$+CHR$(13)+CHR$(10)
994 SAVE QF$,&&QU$,QN,$FFFF
996 RETURN
