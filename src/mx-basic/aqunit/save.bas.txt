100 REM Test SAVE, LOAD, DEL, FILE$, FILEEND
110 QU=0:REM 0=Screen,1=Printer,2=File
130 GOSUB 900:REM Init
140 GOTO 200
150 ON ERROR GOTO 0
152 ON QE GOTO 280,290,300,365,370,385,390,395
154 ON QE-8 GOTO
180 PRINT "Undefined Error Return";QE
190 REM

200 QT$="SAVE program":GOSUB 910
210 P$="testname"
212 QA$="SAVE `"+P$+"`":GOSUB 890
214 SAVE P$
216 P$="TESTNAME.BAS":QA$="FILE$=`"+P$+"`"
218 GOSUB 920
220 T=DEEK($384F):B$=STR$(B)
222 V=DEEK($38D6):V$=STR$(V)
224 L=V-T:L$=STR$(L)
230 QU$="DIM P(1023)":GOSUB 980
232 DIM P(1023)
234 QA$="LOAD `"+P$+"`,&P(0)":GOSUB 890
236 LOAD P$,&P(0)
240 F$="STRING$(12,$FF)"
242 QA$="PEEK$(&P(0),12)="+F$:GOSUB 920
244 QA$="PEEK(&P(0)+12)=0":GOSUB 920
246 QA$="PEEK$(&P(0)+13,6)=`TESTNA`":GOSUB 920
248 QA$="PEEK$(&P(0)+19,12)="+F$:GOSUB 920
260 QT$="No File Name":GOSUB 910
270 QE$="MO":QE=1:QD=0:QU$="SAVE":GOSUB 980
275 ON ERROR GOTO 950:SAVE
280 QE$="MO":QE=2:QD=0:QU$="LOAD":GOSUB 980
285 ON ERROR GOTO 950:LOAD
290 QE$="MO":QE=3:QD=0:QU$="DEL":GOSUB 980
295 ON ERROR GOTO 950:DEL

300 QT$="SAVE/LOAD array":GOSUB 910
302 Z$="":FOR I=0 TO 10:A(I)=I
304 Z$=Z$+STR$(A(I)):NEXT
306 QU$="A()="+Z$:GOSUB 980
310 A$="testname":F$="TESTNAME.CAQ"
312 QA$="SAVE `"+A$+"`,*A":GOSUB 890
314 SAVE A$,*A
316 QA$="FILE$=`"+F$+"`":GOSUB 920
320 DIM B(10):QU$="DIM B(10)":GOSUB 980
322 QA$="LOAD `"+A$+"`,*B":GOSUB 890
324 LOAD A$,*B
326 QA$="FILE$=`"+F$+"`"
328 GOSUB 920
330 QA$="PEEK$($3857,6)=`######`"
335 GOSUB 920
340 QA$="PEEK$(&A(0),40)=PEEK$(&B(0),40)"
345 GOSUB 920
350 QT$="DEL filename":GOSUB 910
352 QA$="DEL "+F$+"":GOSUB 890
354 DEL F$
360 QE$="IO":QE=4:QD=5:ON ERROR GOTO 950
362 QA$="LOAD `"+F$+"`,*B":GOSUB 890:LOAD F$,*B
365 QE$="IO":QE=5:QD=5:ON ERROR GOTO 950
367 QA$="DEL `"+F$+"`,*B":GOSUB 890:DEL F$,*B

370 QT$="LOAD/SAVE Array errors":GOSUB 910
380 QE$="FC":QE=6:QD=0:ON ERROR GOTO 950
382 QA$="SAVE `"+A$+"`,*C":GOSUB 890:SAVE P$,*C
385 QE$="FC":QE=7:QD=0:ON ERROR GOTO 950
387 QA$="LOAD `"+A$+"`,*C":GOSUB 890:LOAD P$,*C
390 QE$="IO":QE=8:QD=7:ON ERROR GOTO 950
392 QA$="LOAD `"+P$+"`,*B":GOSUB 890:LOAD P$,*B
395 QA$="DEL `"+P$+"`":GOSUB 890
397 DEL P$

500 QT$="SAVE/LOAD binary":GOSUB 910
510 A=$0245:L=255:A$=STR$(A):L$=STR$(L)
512 B$="testname.raw":F$="TESTNAME.RAW"
514 QA$="SAVE `"+B$+"`,"+A$+","+L$
516 GOSUB 890:SAVE B$,A,L
518 QA$="FILE$=`"+F$+"`":GOSUB 920
520 QA$="S$=STRING$("+L$+")":GOSUB 890
522 S$=STRING$(L):D=&&S$:D$=STR$(D)
524 QA$="LOAD `"+B$+"`,"+D$:GOSUB 890
526 LOAD B$,D
528 QA$="S$=PEEK$("+A$+","+L$+")":GOSUB 920

530 O=L+1:O$=STR$(O):T=O+O:T$=STR$(T):E=L-1:E$=STR$(E)
534 QA$="SAVE `"+B$+"`,"+A$+"+"+O$+","+L$+","+O$
536 GOSUB 890:REM SAVE B$,A+O,L,O
540 QA$="LOAD `"+B$+"`,&P(0)":GOSUB 890
542 LOAD B$,&P(0)
544 QA$="FILEEND=&P(0)+"+E$:GOSUB 920
546 QA$="PEEK$(&P(0),"+T$+")=PEEK$("+A$+","+T$+")"
548 GOSUB 920

800 REM
810 PRINT "Passed:";QR(1)
820 PRINT "Failed:";QR(0)
870 END
890 GOSUB 970:QU$=QA$:GOTO 980
900 QF$=FILE$:QF=LEN(FILE$):REM Init
902 IF QU>1 THEN DIM Q(999):QP=0
904 QU$="Running "+QF$:GOSUB 980
906 QF$=LEFT$(FILE$,QF-4)+".TST"
907 QR$(0)="Fail: ":QR$(1)="Pass: "
908 RETURN
910 QU$="Testing "+QT$:REM Print Title
912 GOTO 980
920 GOSUB 970:REM Do Assertion
922 ON ERROR GOTO 930
924 QV=-EVAL(QA$):ON ERROR GOTO 0
926 QU$=QR$(QV)+QA$:GOSUB 980
928 QR(QV)=QR(QV)+1:RETURN
930 QU$=ERR$(0)+" Error in "+QA$
932 GOSUB 980
938 END
940 GOSUB 970:REM Assert Error
942 ON ERROR GOTO 950
944 QV=EVAL(QA$):ON ERROR GOTO 0
946 QU$="No Error in "+QA$
948 GOSUB 980:QR(0)=QR(0)+1:RETURN
950 QV=1:QU$=ERR$(0)+" Error in "+QA$
951 IF QD THEN QU$="DOS Error "+ERR$(2)
952 IF ERR$(0)<>QE$ THEN QV=0
954 IF QD AND ERR(2)<>QD THEN QV=0
956 QR(QV)=QR(QV)+1:QU$=QR$(QV)+QU$
958 GOSUB 980:RESUME 150
960 REM so we can RETURN from Error
970 QA$=QA$+" ":REM Convert ` to "
972 FOR QI=0 TO LEN(QA$)-1
974 QC=PEEK(&&QA$+QI)
976 IF QC=96 THEN POKE &&QA$+QI,34
978 NEXT:RETURN
980 IF QU GOTO 988:REM Output Line
982 IF QL>20 THEN QK=KEY(0):QL=0
984 PRINT QU$:QL=QL+1:RETURN
988 IF QU=1 THEN LPRINT QU$:RETURN
996 RETURN
