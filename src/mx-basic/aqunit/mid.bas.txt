100 REM Test STRING$, INSTR, MID$
110 QU=0:REM 0=Screen,1=Printer,2=File
130 GOSUB 900:REM Init
140 GOTO 200
150 ON QE GOTO 272,274,276,278,280,282
160 ON QE-6 GOTO 372,374,376,378,380,382
180 PRINT "Undefined Error Return";QE
190 REM
200 CLS:QT$="STRING$()":GOSUB 910
210 QA$="STRING$(40)=PEEK$($3000,40)":GOSUB 920
212 QA$="STRING$(255,6)=PEEK$($3400,255)":GOSUB 920
220 A=$3001:B=A+9:C=64
222 A$=STR$(A):B$=STR$(B):C$=STR$(C)
224 QU$="POKE"+A$+" TO"+B$+","+C$
226 POKE A TO B,C:GOSUB 980
230 S$="STRING$(9,"+C$+")"
232 P$="PEEK$("+A$+",9)"
234 QA$=S$+"="+P$:GOSUB 920
236 S$="STRING$(9,`"+CHR$(C)+"`)"
238 QA$=S$+"="+P$:GOSUB 920
240 A$="`APPLE`":B$="`AAAAA`"
242 QA$="STRING$(5,"+A$+")="+B$
244 GOSUB 920
270 QA$="STRING$(-1)":QE$="FC":QE=1:GOSUB 940
272 QA$="STRING$(-1,65)":QE$="FC":QE=2:GOSUB 940
274 QA$="STRING$(-1,`A`)":QE$="FC":QE=3:GOSUB 940
276 QA$="STRING$(1,-1)":QE$="FC":QE=4:GOSUB 940
278 QA$="STRING$(1,256)":QE$="FC":QE=5:GOSUB 940
280 QA$="STRING$(256)":QE$="FC":QE=6:GOSUB 940
282 REM

300 QT$="INSTR$()":GOSUB 910
310 N$="`EI`":REM Needle
312 H$="`EIEIO`":REM Needle
314 QA$="INSTR("+H$+","+N$+")=1":GOSUB 920
316 QA$="INSTR(2,"+H$+","+N$+")=3":GOSUB 920
318 QA$="INSTR("+H$+","+N$+"+`O`)=3":GOSUB 920
320 H$=H$+"+"+H$:N$="`OEI`"
322 QA$="INSTR("+H$+","+N$+")=5":GOSUB 920
370 QA$="INSTR(-1,`ABC`,`A`)":QE$="FC":QE=7:GOSUB 940
372 QA$="INSTR(256,`ABC`,`A`)":QE$="FC":QE=8:GOSUB 940
374 QA$="INSTR(1)":QE$="SN":QE=9:GOSUB 940
376 QA$="INSTR(1,`ABC`)":QE$="SN":QE=10:GOSUB 940
378 QA$="INSTR(1,2)":QE$="TM":QE=11:GOSUB 940
380 QA$="INSTR(1,`ABC`,2)":QE$="TM":QE=12:GOSUB 940
382 REM

400 QT$="MID$()":GOSUB 910
410 M$="MIDDLE STRING":M=&M$:QA$="M$=`"+M$+"`":GOSUB 890
415 S$="MODDED"::QA$="S$=`"+S$+"`":GOSUB 890
420 QA$="MID$(M$,1)=S$":GOSUB 890:MID$(M$,1)=S$
425 QA$="M$=`MODDED STRING`":GOSUB 920
430 QA$="MID$(M$,11)=`O`":GOSUB 890:MID$(M$,11)="O"
435 QA$="M$=`MODDED STRONG`":GOSUB 920
440 QA$="MID$(M$,11,3)=`U`":GOSUB 890:MID$(M$,11)="U"
445 QA$="M$=`MODDED STRUNG`":GOSUB 920
450 QA$="MID$(M$,8)=`STR`+`AIGHT`":GOSUB 890
455 MID$(M$,8)="STR"+"AIGHT"
460 QA$="M$=`MODDED STRAIG`":GOSUB 920

        
800 REM
810 PRINT "Passed:";QR(1)
820 PRINT "Failed:";QR(0)
880 END
890 GOSUB 970:QU$=QA$:GOTO 980
900 QF$=FILE$:QF=LEN(FILE$):REM Init
902 IF QU>1 THEN DIM Q(999):QP=0
904 QU$="Running "+QF$:GOSUB 980
906 QF$=LEFT$(FILE$,QF-4)+".TST"
907 QR$(0)="Fail: ":QR$(1)="Pass: "
908 RETURN
910 QU$="Testing "+QT$:REM Print Title
912 GOTO 980
920 GOSUB 970:REM Do Assertion
922 ON ERROR GOTO 930
924 QV=-EVAL(QA$):ON ERROR GOTO 0
926 QU$=QR$(QV)+QA$:GOSUB 980
928 QR(QV)=QR(QV)+1:RETURN
930 QU$=ERR$(0)+" Error in "+QA$
932 GOSUB 980
938 END
940 GOSUB 970:REM Assert Error
942 ON ERROR GOTO 950
944 QV=EVAL(QA$):ON ERROR GOTO 0
946 QU$="No Error in "+QA$
948 GOSUB 980:QR(0)=QR(0)+1:RETURN
950 QU$=ERR$(0)+" Error in "+QA$
952 QV=-(ERR$(0)=QE$):QR(QV)=QR(QV)+1
954 QU$=QR$(QV)+QU$:GOSUB 980
956 CLEAR ERR:ON ERROR GOTO 0
958 GOTO 150
960 REM so we can RETURN from Error
970 QA$=QA$+" ":REM Convert ` to "
972 FOR QI=0 TO LEN(QA$)-1
974 QC=PEEK(&&QA$+QI)
976 IF QC=96 THEN POKE &&QA$+QI,34
978 NEXT:RETURN
980 IF QU GOTO 988:REM Output Line
982 IF QL>20 THEN QK=KEY(0):QL=0
984 PRINT QU$:QL=QL+1:RETURN
988 IF QU=1 THEN LPRINT QU$:RETURN
990 QN=LEN(QU$)+2
992 POKE &Q(0)+QP,QU$
994 QP=QP+QN:POKE &Q(0)-1,13,10
996 RETURN
