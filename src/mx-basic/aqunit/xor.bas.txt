100 REM Test AND(), OR(), XOR() functions
110 QU=0:REM 0=Screen,1=Printer,2=File
130 GOSUB 900:REM Init
140 GOTO 200
150 ON QE GOTO 282,284,286,288,290
152 ON QE-5 GOTO 382,384,386,388,390
154 ON QE-10 GOTO 482,484,486,488,490
180 PRINT "Undefined Error Return";QE
190 REM
200 QT$="AND()":GOSUB 910
210 QA$="AND($FFFF,$FFFF)=$FFFF":GOSUB 920
212 QA$="AND($FFFF,$0000)=$0000":GOSUB 920
214 QA$="AND($7777,$8888)=$0000":GOSUB 920
216 QA$="AND($7777,$FFFF)=$7777":GOSUB 920
218 QA$="AND($7777,$0000)=$0000":GOSUB 920
220 QA$="AND($FF*16,$FFFF)=$FF0":GOSUB 920
222 QA$="AND(2^15, 65535)=32768":GOSUB 920
230 QA$="AND(65535,-65535)=1   ":GOSUB 920

280 QA$="AND(65536,0)":QE$="FC":QE=1:GOSUB 940
282 QA$="AND(0,65536)":QE$="FC":QE=2:GOSUB 940
284 QA$="AND(-65536,0)":QE$="FC":QE=3:GOSUB 940
286 QA$="AND(0,-65536)":QE$="FC":QE=4:GOSUB 940
288 QA$="AND(65536,-65536)":QE$="FC":QE=5:GOSUB 940
290 REM

300 QT$="OR()":GOSUB 910
310 QA$="OR($FFFF,$FFFF)=$FFFF":GOSUB 920
312 QA$="OR($FFFF,$0000)=$FFFF":GOSUB 920
314 QA$="OR($7777,$8888)=$FFFF":GOSUB 920
316 QA$="OR($0000,$8888)=$8888":GOSUB 920
318 QA$="OR($7777,$0000)=$7777":GOSUB 920
320 QA$="OR($FF*16,$000F)=$FFF":GOSUB 920
322 QA$="OR(2^15, 65535)=65535":GOSUB 920
330 QA$="OR(65535,-65535)=65535":GOSUB 920

380 QA$="OR(65536,0)":QE$="FC":QE=6:GOSUB 940
382 QA$="OR(0,65536)":QE$="FC":QE=7:GOSUB 940
384 QA$="OR(-65536,0)":QE$="FC":QE=8:GOSUB 940
386 QA$="OR(0,-65536)":QE$="FC":QE=9:GOSUB 940
388 QA$="OR(65536,-65536)":QE$="FC":QE=10:GOSUB 940
390 REM

400 QT$="XOR()":GOSUB 910
410 QA$="XOR($FFFF,$FFFF)=$0000":GOSUB 920
412 QA$="XOR($FFFF,$0000)=$FFFF":GOSUB 920
414 QA$="XOR($7777,$8888)=$FFFF":GOSUB 920
416 QA$="XOR($7777,$FFFF)=$8888":GOSUB 920
418 QA$="XOR($7777,$0000)=$7777":GOSUB 920
420 QA$="XOR($FF*16,$0FFF)=$00F":GOSUB 920
422 QA$="XOR(2^15, 65535)=32767":GOSUB 920
430 QA$="XOR(65535,-65535)=$FFFE":GOSUB 920

480 QA$="XOR(65536,0)":QE$="FC":QE=11:GOSUB 940
482 QA$="XOR(0,65536)":QE$="FC":QE=12:GOSUB 940
484 QA$="XOR(-65536,0)":QE$="FC":QE=13:GOSUB 940
486 QA$="XOR(0,-65536)":QE$="FC":QE=14:GOSUB 940
488 QA$="XOR(65536,-65536)":QE$="FC":QE=15:GOSUB 940
490 REM

800 REM
810 QU$="Passed:"+STR$(QR(1))
815 GOSUB 980
820 QU$="Failed:"+STR$(QR(0))
825 GOSUB 980
870 END
880 IF QU THEN RETURN
882 PRINT "...Press a key..."
884 QL=0:QK=KEY(0)
886 IF QK=3 THEN STOP
888 RETURN
890 GOSUB 970:QU$=QA$:GOTO 980
900 QF$=FILE$:QF=LEN(FILE$):REM Init
902 IF QU>1 THEN DIM Q(999):QP=0
904 QU$="Running "+QF$:GOSUB 980
906 QF$=LEFT$(FILE$,QF-4)+".TST"
907 QR$(0)="Fail: ":QR$(1)="Pass: "
908 RETURN
910 QU$="Testing "+QT$:REM Print Title
912 GOTO 980
920 GOSUB 970:REM Do Assertion
922 ON ERROR GOTO 930
924 QV=-EVAL(QA$):ON ERROR GOTO 0
926 QU$=QR$(QV)+QA$:GOSUB 980
928 QR(QV)=QR(QV)+1:RETURN
930 QU$=ERR$(0)+" Error in "+QA$
932 GOSUB 980
938 END
940 GOSUB 970:REM Assert Error
942 ON ERROR GOTO 950
944 QV=EVAL(QA$):ON ERROR GOTO 0
946 QU$="No Error in "+QA$
948 GOSUB 980:QR(0)=QR(0)+1:RETURN
950 QU$=ERR$(0)+" Error in "+QA$
952 QV=-(ERR$(0)=QE$):QR(QV)=QR(QV)+1
954 QU$=QR$(QV)+QU$:GOSUB 980
956 CLEAR ERR:ON ERROR GOTO 0
958 GOTO 150
960 REM so we can RETURN from Error
970 QA$=QA$+" ":REM Convert ` to "
972 FOR QI=0 TO LEN(QA$)-1
974 QC=PEEK(&&QA$+QI)
976 IF QC=96 THEN POKE &&QA$+QI,34
978 NEXT:RETURN
980 IF QU GOTO 988:REM Output Line
982 IF QL>20 THEN GOSUB 880
984 PRINT QU$:QL=QL+1:RETURN
988 IF QU=1 THEN LPRINT QU$:RETURN
990 QN=LEN(QU$)+2
992 POKE &Q(0)+QP,QU$
994 QP=QP+QN:POKE &Q(0)-1,13,10
996 RETURN
