100 REM Test POKE, DOKE, DEEK, COPY 
110 QU=0:REM 0=Screen,1=Printer,2=File
130 GOSUB 900:REM Init
150 ON ERROR GOTO 0
152 ON QE GOTO 280,300
180 PRINT "Undefined Error Return";QE
190 REM
200 QT$="POKE":GOSUB 910
210 FOR I=1 TO 3:A=$3000+I
212 QU$="POKE"+STR$(A)+","+STR$(I)
214 GOSUB 980
216 POKE A,I
218 NEXT
220 FOR I=1 TO 3:A=$3000+I
222 QA$="PEEK("+STR$(A)+")="+STR$(I)
224 GOSUB 920
226 NEXT
230 A=$3000+8:A$=STR$(A)
232 QU$="POKE"+A$+",65,66,67,68"
234 POKE A,65,66,67,68:GOSUB 980
236 QA$="PEEK$("+A$+",4)=`ABCD`"
238 GOSUB 920
240 A=$3000+20:A$=STR$(A)
241 B=A+1024:B$=STR$(B)
242 QU$="POKE"+A$+",64,STEP 1024,$76"
244 POKE A,64,STEP 1024,$76:GOSUB 980
246 QA$="PEEK("+A$+")=64":GOSUB 920
248 QA$="PEEK("+B$+")=$76":GOSUB 920
250 C$="&6F":D$=CHR$(34)+C$+CHR$(34)
252 QU$="POKE"+A$+D$+",STEP 1024"+D$
254 POKE A,C$,STEP 1024,C$:GOSUB 980
256 QA$="PEEK$("+A$+",3)="+D$:GOSUB 920
258 QA$="PEEK$("+B$+",3)="+D$:GOSUB 920
270 QA$="POKE STEP 1,32":QE$="SN":QE=1
275 GOSUB 940
280 A=$3000+1:A$=STR$(A)
281 B=$3000+23:B$=STR$(B)
282 QU$="POKE"+A$+" TO"+B$+",32"
284 POKE A TO B,32:GOSUB 980
286 QA$="PEEK$("+A$+",23)="
288 QA$=QA$+"STRING$(23)":GOSUB 920
290 QA$="POKE"+A$+" TO"+B$+",`@`"
292 GOSUB 970:QE$="TM":QE=2
294 ON ERROR GOTO 950
296 POKE A TO B,"@"
300 REM
500 QT$="DOKE & DEEK":GOSUB 910
510 A=$3001:A$=STR$(A):C$="1234"
512 QU$="DOKE"+A$+",$"+C$:GOSUB 980
514 DOKE A,DEC(C$)
516 QA$="DEEK("+A$+")=$"+C$:GOSUB 920
520 A=$3002:A$=STR$(A):C$="CDEF"
522 QU$="DOKE"+A$+",$"+C$:GOSUB 980
524 DOKE A,DEC(C$)
526 QA$="DEEK("+A$+")=$"+C$:GOSUB 920
530 A=$3001:A$=STR$(A):C$="EF34"
532 QA$="DEEK("+A$+")=$"+C$:GOSUB 920
534 QA$="PEEK($3001)=$34":GOSUB 920
536 QA$="PEEK($3002)=$EF":GOSUB 920
538 QA$="PEEK($3003)=$CD":GOSUB 920
600 QT$="COPY":GOSUB 910
610 A=$379:A$=STR$(A):L=16
611 B=$3001:B$=STR$(B):L$=STR$(L)
612 QU$="COPY"+A$+","+B$+","+L$
614 COPY A,B,L:GOSUB 980
616 E$="PEEK$("+A$+","+L$+")"
617 F$="PEEK$("+B$+","+L$+")"
618 QA$=E$+"="+F$:GOSUB 920
620 C=B+L:D=32:C$=STR$(C):D$=STR$(D)
622 QA$="PEEK("+C$+")="+D$:GOSUB 920
800 REM
810 PRINT "Passed:";QR(1)
820 PRINT "Failed:";QR(0)
890 END
900 QF$=FILE$:QF=LEN(FILE$):REM Init
902 IF QU>1 THEN DIM Q(999):QP=0
904 QU$="Running "+QF$:GOSUB 980
906 QF$=LEFT$(FILE$,QF-4)+".TST"
907 QR$(0)="Fail: ":QR$(1)="Pass: "
908 RETURN
910 QU$="Testing "+QT$:REM Print Title
912 GOTO 980
920 GOSUB 970:REM Do Assertion
922 ON ERROR GOTO 930
924 QV=-EVAL(QA$):ON ERROR GOTO 0
926 QU$=QR$(QV)+QA$:GOSUB 980
928 QR(QV)=QR(QV)+1:RETURN
930 QU$=ERR$(0)+" Error in "+QA$
932 GOSUB 980
938 END
940 GOSUB 970:REM Assert Error
942 ON ERROR GOTO 950
944 QV=EVAL(QA$):ON ERROR GOTO 0
946 QU$="No Error in "+QA$
948 GOSUB 980:QR(0)=QR(0)+1:RETURN
950 QU$=ERR$(0)+" Error in "+QA$
952 QV=-(ERR$(0)=QE$):QR(QV)=QR(QV)+1
954 QU$=QR$(QV)+QU$:GOSUB 980
956 RESUME 150
960 REM so we can RETURN from Error
970 QA$=QA$+" ":REM Convert ` to "
972 FOR QI=0 TO LEN(QA$)-1
974 QC=PEEK(&&QA$+QI)
976 IF QC=96 THEN POKE &&QA$+QI,34
978 NEXT:RETURN
980 IF QU GOTO 988:REM Output Line
982 IF QL>20 THEN QK=KEY(0):QL=0
984 PRINT QU$:QL=QL+1:RETURN
988 IF QU=1 THEN LPRINT QU$:RETURN
990 QN=LEN(QU$)+2
992 POKE &Q(0)+QP,QU$
994 QP=QP+QN:POKE &Q(0)-1,13,10
996 RETURN
