100 REM Test LOCATE and POS()
110 QU=0:REM 0=Screen,1=Printer,2=File
130 GOSUB 900:REM Init
140 GOTO 200
150 ON QE GOTO 520,530,540
180 PRINT "Undefined Error Return";QE
190 REM

200 CLS:PRINT:PRINT:PRINT
205 QT$="LOCATE & POS":GOSUB 910
210 QU$="LOCATE 1,1":GOSUB 980
215 LOCATE 1,1
220 DX=POS(1):DY=POS(2):EX=POS(3):EC=POS(4):LOCATE 1,6
225 QU$="DX=POS(1)":GOSUB 980:QA$="DX=1":GOSUB 920
230 QU$="DY=POS(2)":GOSUB 980:QA$="DY=1":GOSUB 920
235 QU$="EX=POS(3)":GOSUB 980:QA$="EX=12369":GOSUB 920
240 QU$="EC=POS(4)":GOSUB 980:QA$="EC=13393":GOSUB 920
245 QA$="EC-EX=1024:GOSUB 920

300 GOSUB 880
305 CLS:LOCATE 1,1
310 PRINT "ABCD1234"
315 DZ=POS(0):DX=POS(1):DY=POS(2):DP$=PEEK$(12369,8)
320 QU$="DZ=POS(0)":GOSUB 980:QA$="DZ=0":GOSUB 920
325 QU$="DX=POS(1)":GOSUB 980:QA$="DX=1":GOSUB 920
330 QU$="DY=POS(2)":GOSUB 980:QA$="DY=2":GOSUB 920
335 QU$="DP$=PEEK$(12369,8)":GOSUB 980:QA$="DP$=$`4142434431323334`":GOSUB 920

400 GOSUB 880
405 CLS:PRINT:PRINT:PRINT
410 QU$="LOCATE 38,23":GOSUB 980
415 LOCATE 38,23
420 DX=POS(1):DY=POS(2):PRINT
425 IF QU=0 THEN LOCATE 1,4
430 QU$="DX=POS(1)":GOSUB 980:QA$="DX=38":GOSUB 920
435 QU$="DY=POS(2)":GOSUB 980:QA$="DY=23":GOSUB 920

500 GOSUB 880
505 CLS
510 QE=1:QE$="FC":QA$="POS(5)":GOSUB 940
520 QE=2:QE$="FC":QA$="POS(-4)":GOSUB 940
530 QE=3:QE$="FC":QA$="POS(`X`)":GOSUB 940
540 REM

800 REM
810 QU$="Passed:"+STR$(QR(1))
815 GOSUB 980
820 QU$="Failed:"+STR$(QR(0))
825 GOSUB 980
870 END
880 IF QU THEN RETURN
882 PRINT "...Press a key..."
884 QL=0:QK=KEY(0)
886 IF QK=3 THEN STOP
888 RETURN
890 GOSUB 970:QU$=QA$:GOTO 980
900 QF$=FILE$:QF=LEN(FILE$):REM Init
902 IF QU>1 THEN DIM Q(999):QP=0
904 QU$="Running "+QF$:GOSUB 980
906 QF$=LEFT$(FILE$,QF-4)+".TST"
907 QR$(0)="Fail: ":QR$(1)="Pass: "
908 RETURN
910 QU$="Testing "+QT$:REM Print Title
912 GOTO 980
920 GOSUB 970:REM Do Assertion
922 ON ERROR GOTO 930
924 QV=-EVAL(QA$):ON ERROR GOTO 0
926 QU$=QR$(QV)+QA$:GOSUB 980
928 QR(QV)=QR(QV)+1:RETURN
930 QU$=ERR$(0)+" Error in "+QA$
932 GOSUB 980
938 END
940 GOSUB 970:REM Assert Error
942 ON ERROR GOTO 950
944 QV=EVAL(QA$):ON ERROR GOTO 0
946 QU$="No Error in "+QA$
948 GOSUB 980:QR(0)=QR(0)+1:RETURN
950 QU$=ERR$(0)+" Error in "+QA$
952 QV=-(ERR$(0)=QE$):QR(QV)=QR(QV)+1
954 QU$=QR$(QV)+QU$:GOSUB 980
956 CLEAR ERR:ON ERROR GOTO 0
958 GOTO 150
960 REM so we can RETURN from Error
970 QA$=QA$+" ":REM Convert ` to "
972 FOR QI=0 TO LEN(QA$)-1
974 QC=PEEK(&&QA$+QI)
976 IF QC=96 THEN POKE &&QA$+QI,34
978 NEXT:RETURN
980 IF QU GOTO 988:REM Output Line
982 IF QL>20 THEN GOSUB 880
984 PRINT QU$:QL=QL+1:RETURN
988 IF QU=1 THEN LPRINT QU$:RETURN
990 QN=LEN(QU$)+2
992 POKE &Q(0)+QP,QU$
994 QP=QP+QN:POKE &Q(0)-1,13,10
996 RETURN
