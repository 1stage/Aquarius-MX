100 REM Test DTM$, SDTM, SLEEP, VER
110 QU=0:REM 0=Screen,1=Printer,2=File
130 GOSUB 900:REM Init
140 GOTO 200
150 ON QE GOTO 
180 PRINT "Undefined Error Return";QE
190 REM


200 QT$="RTC":GOSUB 910
210 QU$="TS$=DTM$(0)":GOSUB 980
215 TS$=DTM$(0)
220 IF TS$<>"" GOTO 300:REM RTC Present
225 QA$="DTM$(0)=``":GOSUB 920
230 PRINT "No RTC present. Skipping clock tests."
235 GOTO 600:REM Skip remaining clock tests

300 GOSUB 880
310 QT$="DTM$":GOSUB 910
320 QA$="LEN(DTM$(0))=14":GOSUB 920
325 QA$="LEN(DTM$(1))=19":GOSUB 920
330 DS$=MID$(DTM$(1),5,1):ES$=MID$(DTM$(1),8,1):REM Hyphens check
335 FS$=MID$(DTM$(1),11,1):REM Space check
340 GS$=MID$(DTM$(1),14,1):HS$=MID$(DTM$(1),17,1):REM Colons check
345 QU$="DS$=MID$(DTM$(1),5,1)":GOSUB 980:QA$="DS$=`-`":GOSUB 920
350 QU$="ES$=MID$(DTM$(1),8,1)":GOSUB 980:QA$="ES$=`-`":GOSUB 920
355 QU$="FS$=MID$(DTM$(1),11,1)":GOSUB 980:QA$="FS$=` `":GOSUB 920
360 QU$="GS$=MID$(DTM$(1),14,1)":GOSUB 980:QA$="GS$=`:`":GOSUB 920
365 QU$="HS$=MID$(DTM$(1),17,1)":GOSUB 980:QA$="HS$=`:`":GOSUB 920

400 GOSUB 880
410 QT$="SDTM":GOSUB 910
415 OT$=DTM$(0):REM Grab original datetime
420 TT$="270214020000":REM 14 FEB 2027 @ 02:00:00 AM
425 SDTM TT$
430 IF (LEFT$(DTM$(0),10) = LEFT$(TT$,10)) GOTO 445:REM Set Time works
435 PRINT "RTC cannot be set. Skipping set tests."
440 GOTO 500
445 QA$="LEFT$(DTM$(0),10) = LEFT$(TT$,10)":GOSUB 920
450 PRINT "Resetting datetime."
455 SDTM (LEFT$(OT$,12))

500 GOSUB 880
505 IF IN($FF)<>$FF GOTO 505
510 QT$="SLEEP":GOSUB 910
515 SS=1000
520 OT=VAL(RIGHT$(DTM$(0),4)):REM Grab original MMSScc0
525 SLEEP SS
530 TT=VAL(RIGHT$(DTM$(0),4)):REM Grab new MMSScc0
535 IF TT<OT THEN TT=TT+6000
540 QU$="OT ="+STR$(OT):GOSUB 980
542 QU$="TT ="+STR$(TT):GOSUB 980
544 QU$="TT - OT ="+STR$(TT-OT):GOSUB 980
550 QA$="(TT-OT)>85":GOSUB 920
555 QA$="(TT-OT)<125":GOSUB 920

600 GOSUB 880
610 QT$="VER":GOSUB 910
615 QA$="VER(0)=512":GOSUB 920
620 QA$="HEX$(VER(0))=`0200`":GOSUB 920

800 REM
810 QU$="Passed:"+STR$(QR(1))
815 GOSUB 980
820 QU$="Failed:"+STR$(QR(0))
825 GOSUB 980
870 END
880 IF QU THEN RETURN
882 PRINT "...Press a key..."
884 QL=0:QK=KEY(0)
886 IF QK=3 THEN STOP
888 RETURN
890 GOSUB 970:QU$=QA$:GOTO 980
900 QF$=FILE$:QF=LEN(FILE$):REM Init
902 IF QU>1 THEN DIM Q(999):QP=0
904 QU$="Running "+QF$:GOSUB 980
906 QF$=LEFT$(FILE$,QF-4)+".TST"
907 QR$(0)="Fail: ":QR$(1)="Pass: "
908 RETURN
910 QU$="Testing "+QT$:REM Print Title
912 GOTO 980
920 GOSUB 970:REM Do Assertion
922 ON ERROR GOTO 930
924 QV=-EVAL(QA$):ON ERROR GOTO 0
926 QU$=QR$(QV)+QA$:GOSUB 980
928 QR(QV)=QR(QV)+1:RETURN
930 QU$=ERR$(0)+" Error in "+QA$
932 GOSUB 980
938 END
940 GOSUB 970:REM Assert Error
942 ON ERROR GOTO 950
944 QV=EVAL(QA$):ON ERROR GOTO 0
946 QU$="No Error in "+QA$
948 GOSUB 980:QR(0)=QR(0)+1:RETURN
950 QU$=ERR$(0)+" Error in "+QA$
952 QV=-(ERR$(0)=QE$):QR(QV)=QR(QV)+1
954 QU$=QR$(QV)+QU$:GOSUB 980
956 CLEAR ERR:ON ERROR GOTO 0
958 GOTO 150
960 REM so we can RETURN from Error
970 QA$=QA$+" ":REM Convert ` to "
972 FOR QI=0 TO LEN(QA$)-1
974 QC=PEEK(&&QA$+QI)
976 IF QC=96 THEN POKE &&QA$+QI,34
978 NEXT:RETURN
980 IF QU GOTO 988:REM Output Line
982 IF QL>20 THEN GOSUB 880
984 PRINT QU$:QL=QL+1:RETURN
988 IF QU=1 THEN LPRINT QU$:RETURN
990 QN=LEN(QU$)+2
992 POKE &Q(0)+QP,QU$
994 QP=QP+QN:POKE &Q(0)-1,13,10
996 RETURN
